# $Id$
# Maintainer: MediaArea.net SARL <info@mediaarea.net>
# This PKGBUILD is for OBS

pkgname=('mediaconch-policy' 'mediaconch-policy-server' 'mediaconch-policy-gui')
pkgver=17.08
pkgrel=1
pkgdesc="Policy checker for video and audio files"
url="http://MediaArea.net/MediaConch"
license=('custom:MPL2' 'GPL3')
source=(mediaconch_${pkgver}.orig.tar.xz)
md5sums=('00000000000000000000000000000000')
arch=('i686' 'x86_64')

# Buildtime dependencies for all packages need to be listed here
makedepends=('desktop-file-utils' 'libxml2' 'libxslt' 'sqlite' 'libevent' 'qt5-base' 'qt5-webkit' 'libzen>=0.4.37' 'libmediainfo>=0.7.99')
# OBS don't provide jansson
#makedepends=(${makedepends[@]} jansson)

prepare() {
    cd "${srcdir}"/MediaConch
    sed -i 's/.$//' *.txt *.html Release/*.txt

    cd "${srcdir}"/MediaConch/Project/GNU/CLI
    chmod u+x autogen.sh
    ./autogen.sh
    ./configure --prefix=/usr --without-jansson

    cd "${srcdir}"/MediaConch/Project/GNU/Server
    chmod u+x autogen.sh
    ./autogen.sh
    ./configure --prefix=/usr --without-jansson

    cd "${srcdir}"/MediaConch/Project/Qt
    chmod u+x prepare
    ./prepare NO_JANSSON=yes USE_WEBKIT=yes
}

build() {
    cd "${srcdir}"/MediaConch/Project/GNU/CLI
    make

    cd "${srcdir}"/MediaConch/Project/GNU/Server
    make

    cd "${srcdir}"/MediaConch/Project/Qt
    make
}

package_mediaconch-policy() {
    pkgdesc="${pkgdesc} (CLI)"
    depends=('libxml2' 'libxslt' 'sqlite' 'libevent' 'libzen>=0.4.37' 'libmediainfo>=0.7.99')

    cd "${srcdir}"/MediaConch/Project/GNU/CLI
    make DESTDIR="${pkgdir}" install-strip

    cd "${srcdir}"/MediaConch
    install -D -m 0644 License.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.html
    install -D -m 0644 License.MPLv2.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.MPLv2.html
    install -D -m 0644 License.GPLv3.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.GPLv3.html

    install -D -m 0644 History_CLI.txt "${pkgdir}"/usr/share/docs/${pkgname}/History.txt
    install -D -m 0644 Release/ReadMe_CLI_Linux.txt "${pkgdir}"/usr/share/docs/${pkgname}/ReadMe.txt
}

package_mediaconch-policy-server() {
    pkgdesc="${pkgdesc} (Server)"
    depends=('libxml2' 'libxslt' 'sqlite' 'libevent' 'libzen>=0.4.37' 'libmediainfo>=0.7.99')
    backup=('etc/mediaconch-policy/MediaConch.rc')

    cd "${srcdir}"/MediaConch/Project/GNU/Server
    make DESTDIR="${pkgdir}" install-strip

    install -D -m 0644 MediaConch.rc "${pkgdir}"/etc/mediaconch-policy/MediaConch.rc
    install -D -m 0644 mediaconch-policyd.service "${pkgdir}"/usr/lib/systemd/system/mediaconch-policyd.service

    cd "${srcdir}"/MediaConch
    install -D -m 0644 License.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.html
    install -D -m 0644 License.MPLv2.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.MPLv2.html
    install -D -m 0644 License.GPLv3.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.GPLv3.html

    install -D -m 0644 Documentation/Daemon.md "${pkgdir}"/usr/share/docs/${pkgname}/Daemon.md
    install -D -m 0644 Documentation/Config.md "${pkgdir}"/usr/share/docs/${pkgname}/Config.md
    install -D -m 0644 Documentation/REST.md "${pkgdir}"/usr/share/docs/${pkgname}/REST.md
}

package_mediaconch-policy-gui() {
    pkgdesc="${pkgdesc} (GUI)"
    depends=('libxml2' 'libxslt' 'sqlite' 'libevent' 'qt5-base' 'qt5-webkit' 'libzen>=0.4.37' 'libmediainfo>=0.7.99')

    cd "${srcdir}"/MediaConch
    install -D -m 0755 Project/Qt/mediaconch-policy-gui "${pkgdir}"/usr/bin/mediaconch-policy-gui

    install -D -m 0644 Source/Resource/Image/MediaConch.png "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/mediaconch-policy.png
    install -D -m 0644 Source/Resource/Image/MediaConch.png "${pkgdir}"/usr/share/pixmaps/mediaconch-policy.png

    install -D -m 0644 Project/GNU/GUI/mediaconch-gui.desktop "${pkgdir}"/usr/share/applications/mediaconch-policy-gui.desktop

    install -D -m 0644 Project/GNU/GUI/mediaconch-gui.kde3.desktop "${pkgdir}"/usr/share/apps/konqueror/servicemenus/mediaconch-policy-gui.desktop
    install -D -m 0644 Project/GNU/GUI/mediaconch-gui.kde4.desktop "${pkgdir}"/usr/share/kde4/services/ServiceMenus/mediaconch-policy-gui.desktop
    install -D -m 0644 Project/GNU/GUI/mediaconch-gui.appdata.xml "${pkgdir}"/usr/share/appdata/mediaconch-policy-gui.appdata.xml

    install -D -m 0644 License.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.html
    install -D -m 0644 License.MPLv2.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.MPLv2.html
    install -D -m 0644 License.GPLv3.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.GPLv3.html

    install -D -m 0644 History_GUI.txt "${pkgdir}"/usr/share/docs/${pkgname}/History.txt
    install -D -m 0644 Release/ReadMe_GUI_Linux.txt "${pkgdir}"/usr/share/docs/${pkgname}/ReadMe.txt
}

